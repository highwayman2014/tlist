
&НаКлиенте
Процедура ЗагрузитьСДиска(Команда)
	
	АдресВХранилище = "";
	ВыбранноеИмяФайла = "";
	
	Если ПоместитьФайл(АдресВХранилище, , ВыбранноеИмяФайла, , УникальныйИдентификатор) Тогда
		
		Файл = Новый Файл(ВыбранноеИмяФайла);
		СсылкаНаФайлВоВременномХранилище = АдресВХранилище;
		Объект.Наименование = Файл.Имя;
		Модифицированность = Истина;

	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Получить файл из хранилища и поместить его в объект.
	Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВоВременномХранилище) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СсылкаНаФайлВоВременномХранилище);
		ТекущийОбъект.ХранилищеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//Удалить файл из временного хранилища
	Если ЭтоАдресВременногоХранилища(СсылкаНаФайлВоВременномХранилище) Тогда
		УдалитьИзВременногоХранилища(СсылкаНаФайлВоВременномХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ПараметрыВыбора.Свойство("СсылкаНаВладельца")
		И Параметры.Ключ.Пустая() Тогда
		Объект.СсылкаНаВладельца = Параметры.ПараметрыВыбора.СсылкаНаВладельца;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПросмотрФайла(Команда)
	
	Если Модифицированность = Истина Тогда
		Сообщить("Внимание! Вначале надо записать элемент справочника 'Файлы'!", СтатусСообщения.Важное);
	Иначе
		СсылкаНаФайлВИБ = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ХранилищеФайла");
		ПолучитьФайл(СсылкаНаФайлВИБ, Объект.Наименование);
	КонецЕсли;
	
КонецПроцедуры

